/* Cycript - Optimizing JavaScript Compiler/Runtime
 * Copyright (C) 2009-2015  Jay Freeman (saurik)
*/

/* GNU Affero General Public License, Version 3 {{{ */
/*
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.

 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.

 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
**/
/* }}} */

/* XXX: supposedly I will be screwed on very very long multi-line comments and need to replace these with a manual lexer. http://websrv.cs.fsu.edu/~engelen/courses/COP5621/Pr2.pdf */

%top{
#if defined(__clang__)
#pragma clang diagnostic push
#pragma clang diagnostic ignored "-Wunused-variable"
#pragma clang diagnostic ignored "-Wdeprecated-register"
#else
#pragma GCC diagnostic push
#pragma GCC diagnostic ignored "-Wsign-compare"
#pragma GCC diagnostic ignored "-Wunused-function"
#pragma GCC diagnostic ignored "-Wunused-variable"
#endif
}

%{

#define YYLTYPE CYLocation
#include "Cycript.tab.hh"
typedef cy::parser::token tk;

#include "Highlight.hpp"

#define YY_EXTRA_TYPE CYDriver *

// do /not/ fold token to the return: this is a macro and the ordering is dependent
#define F(value, highlight) do { \
    int token(value); \
@begin ObjectiveC
    yyextra->no_.AtImplementation = false; \
@end
    yyextra->no_.Function = false; \
    yyextra->no_.OpenBrace = false; \
    yylval->highlight_ = highlight; \
    return token; \
} while (false)

#define P yyextra->pool_
#define A new(P)
#define Y P.strmemdup(yytext, yyleng)

#define I(type, Type, value, highlight) do { \
    yylval->type ## _ = A CY ## Type; \
    F(value, highlight); \
} while (false)

#define T yylval->newline_ = yyextra->state_ == CYNewLine; BEGIN(Div);
#define C T yyextra->state_ = CYClear;
#define R T yyextra->state_ = CYRestricted;

#define N \
    if (yyextra->state_ != CYNewLine) { \
        if (yyextra->state_ != CYRestricted) \
            yyextra->state_ = CYNewLine; \
        else { \
            yyextra->state_ = CYClear; \
            F(tk::NewLine, hi::Nothing); \
        } \
    }

#define V(more) { \
    if (const char *nl = reinterpret_cast<const char *>(memchr(yytext, '\n', yyleng))) { \
        unsigned lines(0); \
        size_t left; \
        do { \
            ++lines; \
            left = yyleng - (nl - yytext) - 1; \
            nl = reinterpret_cast<const char *>(memchr(nl + 1, '\n', left)); \
        } while (nl != NULL); \
        yylloc->step(); \
        yylloc->end.lines(lines); \
        yylloc->end.columns(left); \
        more \
    } else L \
}

#define L { \
    yylloc->step(); \
    yylloc->end.columns(yyleng); \
}

#define M { \
    if (yyextra->commented_) { \
        I(comment, Comment(Y), tk::Comment, hi::Comment); \
    } \
}

#define E(message) { \
    CYDriver::Error error; \
    error.location_ = *yylloc; \
    error.message_ = "syntax error, " message; \
    yyextra->errors_.push_back(error); \
    yyterminate(); \
}

int H(char c) {
    if (c >= '0' && c <= '9')
        return c - '0';
    if (c >= 'a' && c <= 'f')
        return c - 'a' + 10;
    if (c >= 'A' && c <= 'F')
        return c - 'A' + 10;
    return -1;
}

static void U(char *&local, unsigned point) {
    if (false) {
    } else if (point < 0x000080) {
        *local++ = point;
    } else if (point < 0x000800) {
        *local++ = 0xc0 | point >> 0x06 & 0x1f;
        goto one;
    } else if (point < 0x010000) {
        *local++ = 0xe0 | point >> 0x0c & 0x0f;
        goto two;
    } else if (point < 0x110000) {
        *local++ = 0xf0 | point >> 0x12 & 0x07;
        *local++ = 0x80 | point >> 0x0c & 0x3f;
      two:
        *local++ = 0x80 | point >> 0x06 & 0x3f;
      one:
        *local++ = 0x80 | point >> 0x00 & 0x3f;
    } else _assert(false);
}

static void U(char *&local, const char *text, yy_size_t &i) {
    unsigned point;

    char next(text[++i]);
    if (next != '{') {
        point = H(text[i + 0]) << 12 | H(text[i + 1]) << 8 | H(text[i + 2]) << 4 | H(text[i + 3]);
        i += 3;
    } else {
        point = 0;
        for (;;) {
            next = text[++i];
            if (next == '}')
                break;
            point = (point << 4) | H(next);
        }
    }

    U(local, point);
}

#define YY_INPUT(data, value, size) { \
    if (yyextra->data_.eof()) \
        value = YY_NULL; \
    else { \
        yyextra->data_.read(data, size); \
        size_t copy(yyextra->data_.gcount()); \
        value = copy == 0 ? YY_NULL : copy; \
    } \
}

%}

%option prefix="cy"
%option bison-bridge
%option bison-locations
%option nodefault
%option noyywrap
%option noyylineno
%option nounput
%option nounistd
%option 8bit
%option backup
%option batch
%option never-interactive
%option pointer
%option reentrant
%option stack

%option full
%option ecs

U1 [\x00-\x7f]
U0 [\x80-\xbf]
U2 [\xc2-\xdf]
U3 [\xe0-\xef]
U4 [\xf0-\xf4]

HexDigit [0-9a-fA-F]
LineTerminatorSequence \r?\n|\r|\xe2\x80[\xa8\xa9]
WhiteSpace [\x09\x0b\x0c\x20]|\xc2\xa0|\xef\xbb\xbf
UnicodeEscape \\u({HexDigit}{4}|\{{HexDigit}+\})

OctalEscape \\[1-7]|\\[4-7][0-7]|\\[0-3][0-7][0-7]?
StringEscape \\['"\\bfnrtv]|\\0|{OctalEscape}|\\x{HexDigit}{2}|{UnicodeEscape}
StringExtra {StringEscape}|\\{LineTerminatorSequence}
SingleString ([^'\\\n]|{StringExtra})*
DoubleString ([^"\\\n]|{StringExtra})*
StringPrefix '{SingleString}|\"{DoubleString}

@include UnicodeIDStart.l
@include UnicodeIDContinue.l

IdentifierMore [$_]

UnicodeStart {IdentifierMore}|{UnicodeIDStart}
UnicodePart {IdentifierMore}|\xe2\x80[\x8c\x8d]|{UnicodeIDContinue}
UnicodeFail {U2}|{U3}|{U3}{U0}|{U4}|{U4}{U0}|{U4}{U0}{U0}
UnicodeScrap {UnicodePart}*{UnicodeFail}?

IdentifierStart {UnicodeStart}|{UnicodeEscape}
IdentifierPart {UnicodePart}|{UnicodeEscape}
IdentifierFail {UnicodeFail}|\\(u({HexDigit}{0,3}|\{{HexDigit}*))?
IdentifierScrap {IdentifierPart}*{IdentifierFail}?

NonTerminator [^\n]
BackslashSequence \\{NonTerminator}
RegularExpressionFirstChar [^\n*\\/]|{BackslashSequence}
RegularExpressionChar [^\n\\/]|{BackslashSequence}
RegularExpressionFlags {UnicodePart}*
RegularExpressionChars {RegularExpressionChar}*
RegularExpressionBody {RegularExpressionFirstChar}{RegularExpressionChars}

@begin E4X
XMLNameStart [a-zA-Z_:]
XMLNamePart [a-zA-Z0-9.-_:]
XMLName {XMLNameStart}{XMLNamePart}*
@end

%s Div
%s RegExp

@begin E4X
%x XMLContent
%x XMLTag
@end

%%

    /* RegEx {{{ */
<RegExp>\/{RegularExpressionBody}\/{RegularExpressionFlags} L C I(literal, RegEx(Y), tk::RegularExpressionLiteral, hi::Constant);
<RegExp>\/{RegularExpressionBody}\/{RegularExpressionFlags}{UnicodeFail} L E("invalid flags")
<RegExp>\/{RegularExpressionBody}?\\? L E("unterminated regex")
    /* }}} */
    /* Comment {{{ */
#![^\n]* L M
\/\/[^\n]* L M

    /* http://ostermiller.org/findcomment.html */
    /* XXX: unify these two rules using !? */
\/\*!([^*]|[\r\n]|(\*+([^*/]|[\r\n])))*\*+\/ V() C I(comment, Comment(Y), tk::Comment, hi::Comment);
\/\*([^*]|[\r\n]|(\*+([^*/]|[\r\n])))*\*+\/ V(N) M
\/\*([^*]|[\r\n]|(\*+([^*/]|[\r\n])))*\** V() E("invalid comment")
    /* }}} */
    /* Element {{{ */
@begin E4X
<RegExp>"<>"      L F(tk::LeftRight, hi::Structure);
<XMLContent>"</>" L F(tk::LeftSlashRight, hi::Structure);

<RegExp,XMLContent>\<!\[CDATA\[(\n|[^[]|\[[^[]|\[\[[^>])*]]> V() F(tk::XMLCDATA, hi::Constant);
<RegExp,XMLContent>\<!--(\n|[^-]|-[^-])*--> V() F(tk::XMLComment, hi::Comment);
<RegExp,XMLContent>\<?(\n|[^?]|\?[^>])*?> V() F(tk::XMLPI, hi::Meta);

<XMLTag>"="  L F(tk::Equal, hi::Structure);
<XMLTag>">"  L F(tk::Right, hi::Structure);
<XMLTag>"/>" L F(tk::SlashRight, hi::Structure);
<XMLTag>"{"  L F(tk::OpenBrace, hi::Structure);

<XMLTag>\"(\n|[^"])*\"|'(\n|[^'])*' V() F(tk::XMLAttributeValue, hi::Constant);
<XMLTag>{XMLName} L F(tk::XMLName, hi::Identifier);
<XMLTag>[ \t\r\n] V() F(tk::XMLWhitespace, hi::Nothing);

<XMLContent>"{"  L F(tk::OpenBrace, hi::Structure);
<XMLContent>"<"  L F(tk::Left, hi::Structure);
<XMLContent>"</" L F(tk::LeftSlash, hi::Structure);
@end
    /* }}} */
    /* Operator {{{ */
"..."  L C F(tk::PeriodPeriodPeriod, hi::Meta);
".."   L E("invalid operator")

@begin E4X
"::"   L C F(tk::ColonColon, hi::Operator);
".."   L C F(tk::PeriodPeriod, hi::Operator);
@end

@begin E4X ObjectiveC
"@"    L C F(tk::At, hi::Operator);
"#"    L C F(tk::Pound, hi::Operator);
@end

"&"    L C F(tk::Ampersand, hi::Operator);
"&&"   L C F(tk::AmpersandAmpersand, hi::Operator);
"&="   L C F(tk::AmpersandEqual, hi::Operator);
"^"    L C F(tk::Carrot, hi::Operator);
"^="   L C F(tk::CarrotEqual, hi::Operator);
"="    L C F(tk::Equal, hi::Operator);
"=="   L C F(tk::EqualEqual, hi::Operator);
"==="  L C F(tk::EqualEqualEqual, hi::Operator);
"=>"   L C F(yylval->newline_ ? tk::EqualRight_ : tk::EqualRight, hi::Operator);
"!"    L C F(tk::Exclamation, hi::Operator);
"!="   L C F(tk::ExclamationEqual, hi::Operator);
"!=="  L C F(tk::ExclamationEqualEqual, hi::Operator);
"-"    L C F(tk::Hyphen, hi::Operator);
"-="   L C F(tk::HyphenEqual, hi::Operator);
"--"   L C F(yylval->newline_ ? tk::HyphenHyphen_ : tk::HyphenHyphen, hi::Operator);
"->"   L C F(tk::HyphenRight, hi::Operator);
"<"    L C F(tk::Left, hi::Operator);
"<="   L C F(tk::LeftEqual, hi::Operator);
"<<"   L C F(tk::LeftLeft, hi::Operator);
"<<="  L C F(tk::LeftLeftEqual, hi::Operator);
"%"    L C F(tk::Percent, hi::Operator);
"%="   L C F(tk::PercentEqual, hi::Operator);
"."    L C F(tk::Period, hi::Operator);
"|"    L C F(tk::Pipe, hi::Operator);
"|="   L C F(tk::PipeEqual, hi::Operator);
"||"   L C F(tk::PipePipe, hi::Operator);
"+"    L C F(tk::Plus, hi::Operator);
"+="   L C F(tk::PlusEqual, hi::Operator);
"++"   L C F(yylval->newline_ ? tk::PlusPlus_ : tk::PlusPlus, hi::Operator);
">"    L C F(tk::Right, hi::Operator);
">="   L C F(tk::RightEqual, hi::Operator);
">>"   L C F(tk::RightRight, hi::Operator);
">>="  L C F(tk::RightRightEqual, hi::Operator);
">>>"  L C F(tk::RightRightRight, hi::Operator);
">>>=" L C F(tk::RightRightRightEqual, hi::Operator);
"*"    L C F(tk::Star, hi::Operator);
"*="   L C F(tk::StarEqual, hi::Operator);
"~"    L C F(tk::Tilde, hi::Operator);

<Div>"/"  L C F(tk::Slash, hi::Operator);
<Div>"/=" L C F(tk::SlashEqual, hi::Operator);

":"    L C F(tk::Colon, hi::Structure);
","    L C F(tk::Comma, hi::Structure);
"?"    L C F(tk::Question, hi::Structure);
";"    L C F(tk::SemiColon, hi::Structure);

"("    L C F(tk::OpenParen, hi::Structure);
")"    L C F(tk::CloseParen, hi::Structure);

"{"    L C F(yyextra->no_.OpenBrace ? tk::OpenBrace__ : yylval->newline_ ? tk::OpenBrace_ : tk::OpenBrace, hi::Structure);
"}"    L C F(tk::CloseBrace, hi::Structure);

"["    L C F(tk::OpenBracket, hi::Structure);
"]"    L C F(tk::CloseBracket, hi::Structure);
    /* }}} */
    /* Keyword {{{ */
"@error"          L C F(tk::AtError, hi::Error);

@begin Java
"@class"          L C F(tk::AtClass, hi::Meta);
@end

@begin C
"@encode"         L C F(tk::AtEncode, hi::Meta);
@end

@begin ObjectiveC
"@end"            L C F(tk::AtEnd, hi::Meta);
"@false"          L C F(tk::AtFalse, hi::Constant);
"@implementation" L C F(yyextra->no_.AtImplementation ? tk::AtImplementation_ : tk::AtImplementation, hi::Meta);
"@import"         L C F(tk::AtImport, hi::Special);
"@NO"             L C F(tk::AtNo, hi::Constant);
"@null"           L C F(tk::AtNull, hi::Constant);
"@selector"       L C F(tk::AtSelector, hi::Meta);
"@true"           L C F(tk::AtTrue, hi::Constant);
"@YES"            L C F(tk::AtYes, hi::Constant);
@end

@({UnicodeStart}{UnicodeScrap}|{UnicodeFail}) L E("invalid keyword")
    /* }}} */
    /* Highlight {{{ */
"undefined"       L C I(identifier, Identifier("undefined"), tk::Identifier_, hi::Operator);

@begin ObjectiveC
"bool"            L C I(identifier, Identifier("bool"), tk::Identifier_, hi::Type);
"BOOL"            L C I(identifier, Identifier("BOOL"), tk::Identifier_, hi::Type);
"id"              L C I(identifier, Identifier("id"), tk::Identifier_, hi::Type);
"nil"             L C I(identifier, Identifier("nil"), tk::Identifier_, hi::Constant);
"NULL"            L C I(identifier, Identifier("NULL"), tk::Identifier_, hi::Constant);
"SEL"             L C I(identifier, Identifier("SEL"), tk::Identifier_, hi::Type);
@end
    /* }}} */
    /* Reserved {{{ */
"abstract"        L C /*FII*/ I(identifier, Identifier("abstract"), tk::Abstract, hi::Meta);
"await"           L C /*II?*/ I(identifier, Identifier("await"), tk::Await, hi::Meta);
"boolean"         L C /*FII*/ I(identifier, Identifier("boolean"), tk::Boolean, hi::Type);
"break"           L R /*KKK*/ F(tk::Break, hi::Control);
"byte"            L C /*FII*/ I(identifier, Identifier("byte"), tk::Byte, hi::Type);
"case"            L C /*KKK*/ F(tk::Case, hi::Control);
"catch"           L C /*KKK*/ F(tk::Catch, hi::Control);
"char"            L C /*FII*/ I(identifier, Identifier("char"), tk::Char, hi::Type);
"class"           L C /*FFK*/ F(tk::Class, hi::Meta);
"const"           L C /*FFK*/ F(tk::Const, hi::Meta);
"continue"        L R /*KKK*/ F(tk::Continue, hi::Control);
"debugger"        L C /*FKK*/ F(tk::Debugger, hi::Meta);
"default"         L C /*KKK*/ F(tk::Default, hi::Control);
"delete"          L C /*KKK*/ F(tk::Delete, hi::Operator);
"do"              L C /*KKK*/ F(tk::Do, hi::Control);
"double"          L C /*FII*/ I(identifier, Identifier("double"), tk::Double, hi::Type);
"else"            L C /*KKK*/ F(tk::Else, hi::Control);
"enum"            L C /*FFF*/ F(tk::Enum, hi::Meta);
"export"          L C /*FFK*/ F(tk::Export, hi::Meta);
"extends"         L C /*FFK*/ F(tk::Extends, hi::Meta);
"false"           L C /*LLL*/ F(tk::False, hi::Constant);
"final"           L C /*FII*/ I(identifier, Identifier("final"), tk::Final, hi::Meta);
"finally"         L C /*KKK*/ F(tk::Finally, hi::Control);
"float"           L C /*FII*/ I(identifier, Identifier("float"), tk::Float, hi::Type);
"for"             L C /*KKK*/ F(tk::For, hi::Control);
"function"        L C /*KKK*/ F(yyextra->no_.Function ? tk::Function_ : tk::Function, hi::Meta);
"goto"            L C /*FII*/ I(identifier, Identifier("goto"), tk::Goto, hi::Control);
"if"              L C /*KKK*/ F(tk::If, hi::Control);
"implements"      L C /*FSS*/ I(identifier, Identifier("implements"), tk::Implements, hi::Meta);
"import"          L C /*FFK*/ F(tk::Import, hi::Meta);
"in"              L C /*KKK*/ F(yyextra->in_.top() ? tk::In_ : tk::In, hi::Operator);
"instanceof"      L C /*KKK*/ F(tk::InstanceOf, hi::Operator);
"int"             L C /*FII*/ I(identifier, Identifier("int"), tk::Int, hi::Type);
"interface"       L C /*FSS*/ I(identifier, Identifier("interface"), tk::Interface, hi::Meta);
"let"             L C /*IS?*/ I(identifier, Identifier("let"), tk::Let, hi::Meta);
"long"            L C /*FII*/ I(identifier, Identifier("long"), tk::Long, hi::Type);
"native"          L C /*FII*/ I(identifier, Identifier("native"), tk::Native, hi::Meta);
"new"             L C /*KKK*/ F(tk::New, hi::Operator);
"null"            L C /*LLL*/ F(tk::Null, hi::Constant);
"package"         L C /*FSS*/ I(identifier, Identifier("package"), tk::Package, hi::Meta);
"private"         L C /*FSS*/ I(identifier, Identifier("private"), tk::Private, hi::Meta);
"protected"       L C /*FSS*/ I(identifier, Identifier("protected"), tk::Protected, hi::Meta);
"public"          L C /*FSS*/ I(identifier, Identifier("public"), tk::Public, hi::Meta);
"return"          L R /*KKK*/ F(tk::Return, hi::Control);
"short"           L C /*FII*/ I(identifier, Identifier("short"), tk::Short, hi::Type);
"static"          L C /*FS?*/ I(identifier, Identifier("static"), tk::Static, hi::Meta);
"super"           L C /*FFK*/ F(tk::Super, hi::Constant);
"switch"          L C /*KKK*/ F(tk::Switch, hi::Control);
"synchronized"    L C /*FII*/ I(identifier, Identifier("synchronized"), tk::Synchronized, hi::Meta);
"this"            L C /*KKK*/ F(tk::This, hi::Constant);
"throw"           L R /*KKK*/ F(tk::Throw, hi::Control);
"throws"          L C /*FII*/ I(identifier, Identifier("throws"), tk::Throws, hi::Meta);
"transient"       L C /*FII*/ I(identifier, Identifier("transient"), tk::Transient, hi::Meta);
"true"            L C /*LLL*/ F(tk::True, hi::Constant);
"try"             L C /*KKK*/ F(tk::Try, hi::Control);
"typeof"          L C /*KKK*/ F(tk::TypeOf, hi::Operator);
"var"             L C /*KKK*/ F(tk::Var, hi::Meta);
"void"            L C /*KKK*/ F(tk::Void, hi::Operator);
"volatile"        L C /*FII*/ I(identifier, Identifier("volatile"), tk::Volatile, hi::Meta);
"while"           L C /*KKK*/ F(tk::While, hi::Control);
"with"            L C /*KKK*/ F(tk::With, hi::Control);
"yield"           L R /*IS?*/ I(identifier, Identifier("yield"), tk::Yield, hi::Control);

"auto"            L C F(tk::Auto, hi::Meta);
"each"            L C I(identifier, Identifier("each"), tk::Each, hi::Control);
"of"              L C I(identifier, Identifier("of"), tk::Of, hi::Operator);

@begin C
"extern"          L C I(identifier, Identifier("extern"), tk::Extern, hi::Type);
"signed"          L C I(identifier, Identifier("signed"), tk::Signed, hi::Type);
"typedef"         L C I(identifier, Identifier("typedef"), tk::Typedef, hi::Meta);
"unsigned"        L C I(identifier, Identifier("unsigned"), tk::Unsigned, hi::Type);
@end

@begin ObjectiveC
"NO"              L C I(identifier, Identifier("NO"), tk::No, hi::Constant);
"YES"             L C I(identifier, Identifier("YES"), tk::Yes, hi::Constant);
@end

@begin E4X
"namespace"       L C I(identifier, Identifier("namespace"), tk::Namespace, hi::Meta);
"xml"             L C I(identifier, Identifier("xml"), tk::XML, hi::Meta);
@end
    /* }}} */
    /* Identifier {{{ */
{UnicodeStart}{UnicodePart}* L C I(identifier, Identifier(Y), tk::Identifier_, hi::Identifier);

{IdentifierStart}{IdentifierPart}* L C {
    char *value(A char[yyleng + 1]);
    char *local(value);

    for (yy_size_t i(0), e(yyleng); i != e; ++i) {
        char next(yytext[i]);
        if (next != '\\')
            *local++ = next;
        else
            U(local, yytext, ++i);
    }

    *local = '\0';
    I(identifier, Identifier(value), tk::Identifier_, hi::Identifier);
}

({IdentifierStart}{IdentifierPart}*)?{IdentifierFail} L E("invalid identifier")
    /* }}} */
    /* Number {{{ */
0[0-7]+ L C I(number, Number(strtoull(yytext + 1, NULL, 8)), tk::NumericLiteral, hi::Constant);
0[0-9]+ L C I(number, Number(strtoull(yytext + 1, NULL, 10)), tk::NumericLiteral, hi::Constant);

0[xX][0-9a-fA-F]+ L C I(number, Number(strtoull(yytext + 2, NULL, 16)), tk::NumericLiteral, hi::Constant);
0[oO][0-7]+ L C I(number, Number(strtoull(yytext + 2, NULL, 8)), tk::NumericLiteral, hi::Constant);
0[bB][0-1]+ L C I(number, Number(strtoull(yytext + 2, NULL, 2)), tk::NumericLiteral, hi::Constant);

(\.[0-9]+|(0|[1-9][0-9]*)(\.[0-9]*)?)([eE][+-]?[0-9]+)? L C I(number, Number(strtod(yytext, NULL)), tk::NumericLiteral, hi::Constant);
(\.[0-9]+|(0|[1-9][0-9]*)(\.[0-9]*)?)[eE][+-]?{IdentifierScrap} L E("invalid exponent")
(\.?[0-9]|(0|[1-9][0-9]*)\.){IdentifierScrap} L E("invalid number")
    /* }}} */
    /* String {{{ */
'{SingleString}'|\"{DoubleString}\" L C {
    char *value(A char[yyleng]);
    char *local(value);

    for (yy_size_t i(1), e(yyleng - 1); i != e; ++i) {
        char next(yytext[i]);

        if (yytext[i] == '\\')
            // XXX: support more line continuation characters
            if (false) line: {
                yylloc->end.lines(1);
                yylloc->end.columns(yyleng - i);
            } else switch (next = yytext[++i]) {
                case '\n': goto line;

                case '\\': next = '\\'; break;
                case '\'': next = '\''; break;
                case '"': next = '"'; break;
                case 'b': next = '\b'; break;
                case 'f': next = '\f'; break;
                case 'n': next = '\n'; break;
                case 'r': next = '\r'; break;
                case 't': next = '\t'; break;
                case 'v': next = '\v'; break;

                case '0': case '1': case '2': case '3':
                    if (yytext[i + 1] < '0' || yytext[i + 1] > '7')
                        next = H(yytext[i]), i += 0;
                    else if (yytext[i + 2] < '0' || yytext[i + 2] > '7')
                        next = H(yytext[i]) << 3 | H(yytext[i + 1]), i += 1;
                    else
                        next = H(yytext[i]) << 6 | H(yytext[i + 1]) << 3 | H(yytext[i + 2]), i += 2;
                break;

                case '4': case '5': case '6': case '7':
                    if (yytext[i + 1] < '0' || yytext[i + 1] > '7')
                        next = H(yytext[i]), i += 0;
                    else
                        next = H(yytext[i]) << 3 | H(yytext[i + 1]), i += 1;
                break;

                case 'x':
                    U(local, H(yytext[i + 1]) << 4 | H(yytext[i + 2]));
                    i += 2;
                continue;

                case 'u':
                    U(local, yytext, i);
                continue;
            }

        *local++ = next;
    }

    *local = '\0';
    I(string, String(value, local - value), tk::StringLiteral, hi::Constant);
}

{StringPrefix}\\(x.{0,2}|u([^{].{0,3}|\{[^}]*)?|{UnicodeFail})? L E("invalid escape")
{StringPrefix} L E("invalid string")
    /* }}} */

{LineTerminatorSequence} yylloc->step(); yylloc->end.lines(); N
{WhiteSpace} L

<<EOF>> if (yyextra->auto_) { yyextra->auto_ = false; F(tk::AutoComplete, hi::Nothing); } L yyterminate();

. L E("invalid character")

%%

void CYDriver::ScannerInit() {
    cylex_init(&scanner_);
    cyset_extra(this, scanner_);
}

void CYDriver::ScannerDestroy() {
    cylex_destroy(scanner_);
}

CYDriver::Condition CYDriver::GetCondition() {
    switch (yy_top_state(scanner_)) {
        case RegExp:
            return RegExpCondition;
@begin E4X
        case XMLContent:
            return XMLContentCondition;
        case XMLTag:
            return XMLTagCondition;
@end
        default:
            _assert(false);
    }
}

void CYDriver::SetCondition(Condition condition) {
    struct yyguts_t *yyg(reinterpret_cast<struct yyguts_t *>(scanner_));

    switch (condition) {
        case RegExpCondition:
            BEGIN(RegExp);
            break;
@begin E4X
        case XMLContentCondition:
            BEGIN(XMLContent);
            break;
        case XMLTagCondition:
            BEGIN(XMLTag);
            break;
@end
        default:
            _assert(false);
    }
}

void CYDriver::PushCondition(Condition condition) {
    switch (condition) {
        case RegExpCondition:
            yy_push_state(RegExp, scanner_);
            break;
@begin E4X
        case XMLContentCondition:
            yy_push_state(XMLContent, scanner_);
            break;
        case XMLTagCondition:
            yy_push_state(XMLTag, scanner_);
            break;
@end
        default:
            _assert(false);
    }
}

void CYDriver::PopCondition() {
    yy_pop_state(scanner_);
}

#if defined(__clang__)
#pragma clang diagnostic pop
#else
// must not pop -Wunused-function
//#pragma GCC diagnostic pop
#endif
